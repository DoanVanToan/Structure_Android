apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../autodimension.gradle'

def APP_ID = "com.fstyle.androidstructure";

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    applicationId "${APP_ID}"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode rootProject.ext.versionCode
    versionName rootProject.ext.versionName

    vectorDrawables.useSupportLibrary = true
  }

  dataBinding {
    enabled = true
  }

  signingConfigs {
    debug {}
    release {
      def signFile = file("../local.properties")
      def signingProps = new Properties()

      // To pass CI
      try {
        signingProps.load(new FileInputStream(signFile))

        storeFile file("../keystores/keystore_release.keystore")
        storePassword signingProps['storePassword']
        keyAlias signingProps['keyAlias']
        keyPassword signingProps['keyPassword']
      } catch (ex) {
      }
    }
  }

  buildTypes {
    release {
      shrinkResources true
      minifyEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  productFlavors {
    dev {
      versionNameSuffix = "dev"
      applicationIdSuffix = ".dev"
    }
    stg {
      versionNameSuffix = "stg"
      applicationIdSuffix = ".stg"
    }
    prod {
      versionName = rootProject.ext.versionNameRelease
      versionCode = rootProject.ext.versionCodeRelease
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  Map<String, String> dependencies = rootProject.ext.dependencies;

  compile dependencies.constraintLayout
  compile dependencies.appCompatV7
  compile dependencies.recyclerview

  compile dependencies.rxJava
  compile dependencies.rxAndroid

  testCompile dependencies.junit
  testCompile dependencies.mockito

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}
